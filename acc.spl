# ATR Copy Center
# 2008-2014 Carsten Strotmann up to V1.3.1
# 2014-2015 Thomas Grasel
# 2015/08/08

const bl $20 ( blank )

const rd $40
const wr $80

const ddevic $0300
const dunit  $0301
const dcmnd  $0302
const dstats $0303
const dbuf   $0304
const dtimlo $0306
const dbyt   $0308
const daux   $030A

const memtop $02E5 ( last free memory location )
const memlo  $02E7 ( first free memory location )
const appmhi $0E
const chkey  $02FC
const consol $D01F

var percom 12
var drvstat 17
var buf   $100
var dpath $100
var secbuf    2
var bufbase   2
var bufsize   2
var bufend    2
var secbuforg 2
var hsbase    2
var hss2u     2
var hsdrive   2
var siov      2
var siolen    2
var tracks    2
var secpertrack 2
var errorcode 1
var adrstr1   2
var adrstr2   2
var lenstr1   1
var lenstr2   1

var devid  1
var devnm  1
var bytsec 2
var sectors 2
var sector  2
var autoinc 1
var side   1
var txtgen 1
var i      2
var fcount 2
var cdigit 2
var sidetgl 1
var cnt    2
var debugflg 1

str stcreatedir "create dir"
str stpathname "\ACC"
str stfilen "\ACC\FILE0000.ATR"
str sterror " Error"
str stwhile "while"
str streading "reading"
str stwriting "writing"
str stsector "Sector"
str stsize "size"
str stillegal "illegal"
str stcreating "creating"
str stmounting "mounting"
str stremoving "removing"
str stmissing "missing"
str statr "ATR"
str sttxt "TXT"
str stsingle "single"
str stmedium "medium"
str stdouble "double"
str stauto "Auto"
str stincrement "inc"
str stdrive "Drive"
str ststatus "status"
str sttimeout "Timeout"
str stwd2793 "WD2793"
str stsrc "Src"
str stdest "Mnt:D3:"
str stdensity "Density"
str stdigit "Digit"
str ston "On "
str stoff "Off"
str stmake "make"
str stmount "mount"
str stcopy "Copy"
str stread "Read "
str stwrite "Write"
str stzeros0 "00000"
str stzeros1 "00000"
str stzeros2 "00000"
str stcompleted "completed"
str stsuccessfully "successfully"
str stunsuccessfully "unsuccessfully"
str stcenter "Center"
str stdots "..."
str sttracks "Tracks"
str ststep "Step"
str stdir "Directory"
str strate "Rate"
str stsectrack "Sec/Track"
str stsides "Sides"
str stbytessec "Bytes/Sec"
str stencoding "Encoding"
str stside "Side"
str stmenumsg "[M] for menu"
str stnextcpy "G or [START] to start next copy"
str sthighspeed "high speed"
str stnormalspeed "normal speed"
str stfn83 "12345678"

: debug
  dup 20 20 at "debug:" disp dup .$ "  "  disp "  " disp .
;

: debugk
  dup 20 20 at "debug:" disp dup .$ "  "  disp "  " disp . key drop
;

: dispspace
  disp space
;

: init ( -- )
  0 debugflg c!
  $44 devid c!
  1 devnm c!
  $80 bytsec !
  18 secpertrack ! ( TG )
  720 sectors !
  1 autoinc c!
  0 side c!
  0 txtgen c!
  0 sidetgl c!
  clearfcount
  4 cdigit !
  end@ secbuf !
  $E459 hss2u !
  $E459 hsdrive !
  $E459 siov !
  end@ appmhi !
;

: lcls ( y -- )
  crsoff 0 at 156 emit ;

: insline ( y -- )
  0 at 157 emit ;

: clearline ( y -- )
  39 i ! {
     dup i at 31 emit
     i --
     i @ 0 = if break then
  }
  drop
;

: rcls ( pos lines -- )
  i ! {
    dup lcls
    i --
    i @ 0 = if break then
  }
  drop
;

: line ( y -- )
  40 * $58 @ + 40 $52 fill ;

: colon ( -- )
  $3A emit ;

data bittable
     $01 $02 $04 $08
     $10 $20 $40 $80
end

: bits ( n -- bm )
  bittable + c@ ;

code scopy
[    ldy #5 ]
[    jmp sc1 ]
end
code sc1
[    lda stzeros1,y  ]
[    clc    ]
[    adc #1 ]
[    sta stzeros1,y ]
[    cmp #$3A ]
[    bne sc2  ]
[    lda #$30 ]
[    sta stzeros1,y ]
[    dey ]
[    bne sc1 ]
[    jmp sc2 ]
end
code sc2
[    rts     ]
end

: sio ( -- rc )
  siov @ call
  dstats c@
;

: secbuffetch ( -- )
  secbuf @ ;

: clearbuf
  buf $FF $FF fill
  0 buf c!
  0 buf $7F + c!
;

: buf1 ( -- addr )
  buf 1+ ;

: preparesio
  $31 ddevic c!
  devnm c@ dunit c!
  $07 dtimlo c!
  0 daux !
;

: writecmd
  wr dstats c!
  sio
;

: readcmd
  rd dstats c!
  sio
;

: getstatus ( -- rc )
  preparesio
  $53 dcmnd c!
  drvstat dbuf !
  4 dbyt !
  readcmd
;

: getsiolen ( n -- l )
  preparesio
  $68 dcmnd c!
  dunit c!
  secbuf @ dbuf !
  2 dbyt !
  readcmd drop
  secbuf @ @
  dup siolen !
;

: gethssio ( addr n -- )
  dup getsiolen dbyt !
  preparesio
  dunit c!
  $69 dcmnd c!
  dup dbuf ! daux !
  $07 dtimlo c!
  readcmd
;

: gets2usio ( -- )
  appmhi @ 3 gethssio
  1 = if
     appmhi @ hss2u !
     siolen @ appmhi +!
  then
;

: getdrvsio ( -- )
  hsbase @ appmhi !
  appmhi @ devnm @ gethssio
  1 = if
     appmhi @ hsdrive !
     siolen @ appmhi +!
  then
  gets2usio
;

: cprint ( addr -- )
  c@ u. ;

: cprintplus ( addr n -- )
  + cprint ;

: printstatus
  stdrive disp
  ststatus disp colon drvstat cprint
  "    " disp stwd2793 disp colon drvstat 3 cprintplus cr
  sttimeout disp colon drvstat 2 cprintplus
;

: mdquery ( tests for medium density )
  getstatus drop drvstat c@ $80 and
  dup 0 > if
    1040 sectors !
    $80 bytsec !
    26 secpertrack !
  then ;

: clearpercom
  percom 12 0 fill ;

: getsectors ( -- sec )
  percom c@ percom 2 + @ >< *
;

: getpercom ( -- rc )
  clearpercom  preparesio
  $4E dcmnd c!
  percom dbuf !
  12 dbyt !
  readcmd getsectors sectors !
;

: printpercom ( -- )
     sttracks disp colon                percom cprint
  cr ststep disp strate disp colon      percom 1 cprintplus
  cr stsectrack disp colon              percom 2+ @ >< u.
  cr stsides disp colon                 percom 4 cprintplus
  cr stencoding disp colon              percom 5 cprintplus
  cr stbytessec disp colon              percom 6 + @ >< u.
  cr stdrive disp ston disp colon       percom 8 cprintplus
  cr stsector disp colon                getsectors u.
;

# sio2usb code

: temp2dpath
  stfilen dpath strcpy
;

: dpath2buf
  dpath buf strcpy
;

: s2ucmd
  $71 ddevic c! 0 dunit c! $80 dbyt !
;

: s2ucmdp
  ddevic c@ $71 =
;

: gets2ustatus
  preparesio s2ucmd
  17 dbyt !
  drvstat dbuf !
  0 dcmnd c!  # get U status info
  readcmd
;

: strappend ( str1 str2 -- )
  dup
  c@ lenstr2 c!
  adrstr2 !
  dup
  c@ lenstr1 c!
  adrstr1 !

  lenstr1 c@ lenstr2 c@ + adrstr1 @ c!
  lenstr2 c@ 1 + lenstr2 c!
  {
    adrstr1 @ lenstr1 c@ + lenstr2 c@ +
    adrstr2 @ lenstr2 c@ + c@ swap c!
    lenstr2 c@ 1 - dup lenstr2 c!
    0 = if
      exit
    then
  }
;

: tofn83 ( addr cnt -- n )
  cntfn83 c!
  cntfn83 c@ stfn83 @ c!
  {
    dup c@ stfn83 @ cntfn83 c@ + c!
    cntfn83 c@ 1 - dup cntfn83 c!
    0 = if
    exit
    then
  }
  drop
;

: writedirentry ( direntryno -- )
  clearbuf

  $10 * bufbase @ +
  dup @ 0 = if drop exit then
  dup @ $80 and if drop exit then
  dup @ $20 and if $2A else $20 then buf $80 + 17 + c!
  $20 buf $80 + 18 + c! ( space )
  $20 buf $80 + 30 + c! ( space )
  1+  dup @ buf $80 + 31 + swap 0 &.   ( Length )
  buf $80 + 31 + dup 7 + swap 3 cmove>
 ( 3 +  dup @ .$ space )  ( Start Sector )
  4 +  dup buf $80 + 18 +  8 cmove     ( Filename )
  8 +      buf $80 + 27 +  3 cmove     ( Extender )
       $2e buf $80 + 26 + c!           ( . )

  buf $80 + 18 + i ! {
    i @ c@ dup $ff = if drop break then ( stop on $FF )
    $7F and i c!                 ( mask msb    )
    i ++                         ( next char   )
  }
 (  buf $80 + 35 type cr )

  cnt @
  preparesio s2ucmd
  $13 dcmnd c!
  dup 1 >> daux !                   # no of 64byte blocks
  $07 dtimlo c!
  48 buf $80 + c!                   # laenge des Records
  1 and if 32 else 0 then buf $80 + 1+ c! # offset
  buf $80 +  2 + 15 erase
  buf $80 + 34 + 13 $20 fill
  $0A0D buf $80 + 47 + !
  buf $80 + dbuf !
  writecmd
  cnt ++

;

: writetxtfile

  clearbuf
  0 cnt !
  bufbase @ secbuf !
  360 sector !
  {
    1 sector +!
    sector @ readsec drop
    0 i !
    {
    (  i @ .$ cr )
      i @ writedirentry
      i ++
      i @ 8 = if break then
    }
    sector @ 368 = if break then
  }

 ( 22 2 at " press key" disp key drop )
;

: maketxtfile
  clearbuf dpath2buf
  $54 buf dup c@ + 2 - c!
  $58 buf dup c@ + 1 - c!
  $54 buf dup c@ + 0 - c!
  preparesio s2ucmd
  $11 dcmnd c!
  $08 daux !
  $07 dtimlo c!
  buf dbuf !
  writecmd
;

: makeatr
  clearbuf dpath2buf
  preparesio s2ucmd
  sectors @ dup >< daux !
  $FFFF = if 1 daux ! then
  bytsec @ $100 = if
    daux c@ $80 or daux c!
  else
    daux c@ $7F and daux c!
  then
  $07 dcmnd c! buf dbuf !
  $10 dtimlo c! writecmd
;

: rmatr
  clearbuf dpath2buf
  preparesio s2ucmd
  $09 dcmnd c!
  $00 daux   !
  buf dbuf !
  $10 dtimlo c!
  writecmd
;

: mountatr
  clearbuf dpath2buf
  preparesio s2ucmd
  $BB daux c!
  $05 dcmnd c!
  buf dbuf !
  $07 dtimlo c!
  writecmd
;

: seccmd ( i cmd -- rc )
  preparesio dcmnd c!
  dup 4 < if $80 else bytsec @ then dbyt !
  daux ! secbuf @ dbuf !
;

: readsec ( i -- )
  $52 seccmd devnm @ dunit c! readcmd
;

: writesec ( i -- )
  $50 seccmd 3 dunit c! writecmd
;

code fullquery ( checks if sector buffer is empty )
[    lda secbuf  ]
[    sta ta      ]
[    lda secbuf+1 ]
[    sta ta+1    ]
[    ldy #0      ]
[    tya         ]
end
code fq1
[    ora (ta),y	 ]
[    iny         ]
[    cpy bytsec  ]
[    bne fq1	 ]
[    jmp push0a  ]
end

# user interface code

: clearfcount
  0 fcount !
;

: toggleautoinc
  autoinc c@ if
    incdigit
  else
    1 autoinc ctoggle
    1 cdigit !
    clearfcount
    printfile
  then
;

: qclearfcount ( n -- )
  fcount @ swap > if clearfcount then
;

: printfile
    cdigit @ 1 = if 9     qclearfcount then
    cdigit @ 2 = if 99    qclearfcount then
    cdigit @ 3 = if 999   qclearfcount then
    cdigit @ 4 = if 9999  qclearfcount then
    fcount @ >outbuf
    cdigit @ i ! {
        $30 ( ASCII 0 ) dpath dup c@ + 3 sidetgl c@ + i @ + - c!
        i -- i @ 0 = if break then
    }
    $A1  ( source = outbuf+1 )
    dpath dup c@ + $A0 c@ 3 sidetgl c@ + + - ( dest )
    $A0 c@ cmove
;

: nextfile
  cdigit @ if
    fcount ++
    printfile
  then
;

: prevfile
  cdigit @ if
    fcount --
    fcount @ $FFFF = if 9999 fcount ! then
    printfile
  then
;

: nextside
  side c@ if 65 else 66 then dpath dup c@ + 4 - c!
  1 side ctoggle
;

: sidetgl_switch
  1 sidetgl ctoggle
  sidetgl c@ if
    0 side c!
    65 dpath dup c@ + 4 - c!
  else
    48 dpath dup c@ + 4 - c!
  then
  printfile
;

: toggleside
  1 sidetgl ctoggle
  sidetgl c@ if nextside then
;

: txtmodesel
  1 txtgen ctoggle
;

: printinfo
  $00 sectors !
  $80 bytsec !
  18 secpertrack !
  stdensity disp colon mdquery 0 >
  if stmedium disp
  else
    getpercom
    percom 2+ @ >< tracks !
    percom 6 + @ 1 = if
      stdouble disp $100 bytsec !
    else
      stsingle disp
    then
  then
  space stsector disp
  $73 emit colon
  sectors @ u. space space
;

: printsource
  4 2 at stsrc disp colon $44 emit
  devnm c@ $30 + emit colon
  space printinfo
;

: printdestination
  crsoff 6 2 at stdest disp space dpath disp " " disp
;

: printautoinc
  crsoff 8 2 at stauto disp stincrement disp colon
  autoinc c@ if ston else stoff then disp
  8 14 at stdigit disp colon
  autoinc c@ if cdigit @ else 0 then u.
  8 22 at stside disp colon
  sidetgl c@ if ston else stoff then disp
;

: printtxtmode
  crsoff 8 31 at "Txt" disp colon
  txtgen c@ if ston else stoff then disp
;

: incdigit
  cdigit ++
  cdigit @ 5 = if
    0 autoinc c!
    0 cdigit !
  else
    1 autoinc c!
  then
;

: invertcursor ( i -- )
  dup dpath + c@ $80 xor
  swap dpath + c! printdestination
;

: inccursor
  i @ dpath c@ < if i ++ then
;

: deccursor
  i @ 6        > if i -- then
;

: newname
  6 i !
  {
    i @ invertcursor
    key
    i @ invertcursor
    dup  47 = if drop 92 then     ( transpose / to \ )
    dup $27 = if break then  ( ESC )
    dup $9B = if break then  ( RETURN )
    dup  42 = if inccursor then  ( cursor right )
    dup  43 = if deccursor then  ( cursor left  )
    dup 157 = if dpath c@ 30 < if
                  i @ dpath + dup 1+ dpath c@ i @ - 1+ cmove>  ( INSERT )
                  bl i @ dpath + c!
                  dpath c@ 1+ dpath c! ( dpath c++ )
                then
              then
    ( dup $2F $59 within )
    dup $2C > if dup $5D < if dup dpath i @ + c! inccursor then then ( ALPHANUMERICAL )
    dup 126 = if dpath c@ i @ - 0 > if   ( DELETE )
                  i @ dpath + dup 1+ swap dpath c@ i @ - cmove
                  dpath c@ 1- dpath c!  ( dpath c-- )
                then
              then
    125 = if 1 dpath c! 47 dpath 1+ c! then ( CLEAR )
  }
  drop
;

: beep
  $FD emit
;

: clserr
  19 lcls 19 lcls 19 lcls 19 lcls
;

: errorwaitkey
  beep key drop clserr
;

: errorhead
  10 13 rcls
  20 1 at sterror dispspace dstats c@ u. space
;

: prints2ustatus
  21 2 at gets2ustatus "Sio2USB:" disp drvstat 15 + c@  dup u. $2F emit $24 emit dup .$ space
  22 2 at
     dup $2E = if "Illegal characters in filename" disp then
     dup $32 = if "USB device is not mounted" disp then
     dup $34 = if "ATR filename not allowed" disp then
     dup $35 = if "Directory not found, create?" disp then
     dup $37 = if "Filename already exists, overwrite?" disp then
     dup $38 = if "Not enough free memory on storage" disp then
     dup $39 = if "No free root directory entry found" disp then
     drop
;

: printerr
  errorhead
  stwhile dispspace dispspace stsector dispspace
  s2ucmdp if
    prints2ustatus
  then key drop
;

: secerr
  errorhead
  stillegal dispspace stsector dispspace stsize dispspace
  key drop
;

: readerr
 streading printerr
;

: writeerr
 stwriting printerr
;

: makeatrerr
  errorhead
  stwhile dispspace stcreating dispspace statr dispspace
  prints2ustatus
  beep key clserr
;

: maketxterr
  errorhead
  stwhile dispspace stcreating dispspace sttxt dispspace
  prints2ustatus
  beep key clserr
;

: cdaccerr
  errorhead
  stmissing dispspace stpathname dispspace
  prints2ustatus
  beep key clserr
;

: rmerr
  errorhead
  stwhile dispspace stremoving dispspace statr dispspace
  prints2ustatus
  beep key clserr
;

: gendirerr ( -- errflag )
  gets2ustatus drop drvstat 15 + c@ $37 = if 0 exit then
  errorhead
  stwhile dispspace stcreatedir dispspace stpathname dispspace
  21 2 at "Sio2USB:" disp drvstat 15 + c@ dup u. $2F emit $24 emit .$ space
  beep key clserr
  1
;

: mounterr
  errorhead
  stwhile dispspace stmounting dispspace statr dispspace
  prints2ustatus errorwaitkey
;

: resetcounter
  stzeros0 stzeros1 strcpy
;

: checkhighspeed ( addr -- )
  @ $E459 = 0if sthighspeed else stnormalspeed then disp
;

: setsecbufbase
  appmhi @ $FF00 and $400 + bufbase !
  $7000 bufbase @ - bufsize !
  bufbase @ bufsize @ + bufend !
  $7000 appmhi !
  $7000 hsbase !
;

: readbuffer
  hsdrive @ siov !
  bufbase @ secbuf !
  stzeros1 stzeros2 strcpy
  sector @ i ! {
       i ++
       scopy 19 15 at stzeros1 disp debugflg @ if space secbuf @ .$ then # key drop
       i @ readsec errorcode c!
       bytsec @ secbuf +!
       sectors @ i @ = if break then                   # last sector
       bufend @ secbuf @ = if break then               # buffer full?
       errorcode c@ $7F > if break then                # error during read?
  }
;

: writebuffer
  hss2u @ siov !
  bufbase @ secbuf !
  stzeros2 stzeros1 strcpy
  sector @ i ! {
       i ++
       scopy
       fullquery 0 = 0if
         19 15 at stzeros1 disp debugflg @ if space secbuf @ .$ then  # key drop
         i @ writesec errorcode c!
       then
       bytsec @ secbuf +!
       sectors @ i @ = if break then              # last sector
       bufend @ secbuf @ = if break then          # buffer full?
       errorcode c@ $7F > if break then           # error during write?
  }
  i @ sector !
;

: cdaccdir ( -- errflag )

  clearbuf ( cd \ATR )
  stpathname buf strcpy
  preparesio s2ucmd
  $00 daux !
  $03 dcmnd c!
  buf dbuf !
  $07 dtimlo c!
  writecmd
    1 > if
      cdaccerr $59 = 0if
        exit
      else
        genaccdir
      then
    else
      0
    then
;

: genaccdir ( -- errflag )
  clearbuf
  stpathname buf strcpy
  preparesio s2ucmd
  $00 daux !
  $07 dcmnd c!
  buf dbuf !
  $07 dtimlo c!
  writecmd
    1 > if gendirerr else 0 then
;

: wrtxt ( -- )
  11 12 rcls

  16 2 at stmake dispspace sttxt disp stdots disp
  maketxtfile 1 > if
    maketxterr $59 = 0if
      exit
    else
      clserr
      18 2 at "removing" dispspace sttxt disp stdots disp
      preparesio s2ucmd
      $09 dcmnd c!
      $00 daux   !
      buf dbuf !
      $10 dtimlo c!
      writecmd
      1 > if rmerr exit then
      maketxtfile  1 > if maketxterr exit then
    then
  then
  20 2 at stcopy dispspace stdir dispspace stdots disp
  writetxtfile
;

: copy
  11 12 rcls
  printsource
  sectors @ not if
    secerr exit
  then
  19 2 at stmake dispspace statr disp stdots disp
  makeatr 1 > if
    makeatrerr $59 = 0if
      exit
    else
      clserr
      19 2 at "removing" dispspace statr disp stdots disp
      rmatr 1 > if rmerr exit then
      makeatr  1 > if makeatrerr exit then
    then
  then
  19 2 at stmount dispspace statr disp stdots disp
  mountatr 1 > if mounterr exit then
  19 8 at stsector dispspace stzeros1 disp
  15 2 at "Drive "   disp devnm c@ $30 + emit space  hsdrive checkhighspeed
  debugflg @ if space hsdrive @ .$ then
  16 2 at "SIO2USB " disp hss2u checkhighspeed
  debugflg @ if space hss2u @ .$ then
  debugflg @ if
    12 2 at "Buffersize:" dispspace bufsize @ .$
    13 2 at "Bufend:" dispspace bufend @ .$
    14 2 at "APPMHI:" dispspace appmhi @ .$
  then
  bufbase @ secbuf !
  resetcounter
  1 errorcode !
  0 sector !
  {
    19 2 at stread disp
    readbuffer errorcode c@ 1 > if readerr then
    errorcode c@ $80 < if
      19 2 at stwrite disp
      writebuffer errorcode c@ 1 > if writeerr then
    then
    sectors @ sector @ = if break then
    errorcode c@ $7F > if break then
  }
  $E459 siov ! ( reset to original SIOV )
  clserr stcopy dispspace
  errorcode c@ $80 < if stsuccessfully else stunsuccessfully then dispspace
  stcompleted dispspace cr cr stnextcpy dispspace
  autoinc c@ if
      sidetgl c@ if
         side c@ if nextside nextfile else nextside then
      else
         nextfile
      then
      printdestination
  then
;

: status
  11 12 rcls
  getstatus drop 11 2 at printstatus
  getpercom drop 14 2 at printpercom
;

: printdirentry
  cnt @ 11 +
  dup 20 > if 10 - 22 else 2 then at
  $10 * bufbase @ +
  dup @ 0 = if drop exit then
  dup @ $80 and if drop exit then
  cnt ++
  dup @ $20 and if $2A emit else space then space
(  1+  dup @ .$ space ) ( Length )
(  2+  dup @ .$ space ) ( Start Sector )
  5 +  dup   8 type     ( Filename )
          $2e emit      ( . )
  8 +       3 type cr   ( Extender )
;

: showdirectory
  11 12 rcls
  0 cnt !
  bufbase @ secbuf !
  360 sector !
  {
    1 sector +!
    sector @ readsec drop
    0 i !
    {
      cnt @ 22 = if 22 2 at " press key" disp key drop 0 cnt ! 11 12 rcls then
      i @ printdirentry
      i ++
      i @ 8 = if break then
    }
    sector @ 368 = if break then
  }
  22 2 at " press key" disp key drop
;

: ver
  "1.4.0 SIO2USB" disp
;

: banner
  cls
  $90 $2C6 c! crsoff
  statr disp stcopy disp stcenter disp
  " (c) 2015 C. Strotmann" disp cr
  "Version " disp ver
  printsource         3 line
  printdestination    5 line
  printautoinc
  printtxtmode        7 line
                      9 line
  1 24 at stmenumsg disp
;

: menu
  10 13 rcls
  11 2 at "1-9 : select source and query disk" disp
  12 2 at " E  : edit ATR filename" disp
  13 2 at " S  : enable/disable diskside A/B" disp
  14 2 at " C  : change diskside A/B" disp
  15 2 at " A  : autoincrement / digits" disp
  16 2 at "+/- : inc-/decrement number" disp
  17 2 at " T  : generate text file" disp
  18 2 at " F  : floppy status" disp
  19 2 at " D  : display DOS 2.x directory" disp
  20 2 at " Q  : quit program" disp
  22 2 at " G  : generate ATR file or [START]" disp
;

: acc ( -- )
  setsecbufbase
  1 devnm c! getdrvsio

  temp2dpath banner menu
  {
   ( crsoff key crsoff )
     crsoff
     {
	0 $4d c! ( ATTRACT Mode reset )
	chkey c@ $FF = if
         consol c@ 6 = if ( [START] )
           $47 ( Simulate G press )
           break
         then
         consol c@ 3 = if ( [OPTION] )
           $4D ( Simulate M press )
           break
         then
       else
         key
         break
       then
     }
     crsoff
     dup $30 > if
         dup $3A < if
             dup $30 - devnm c! printsource getdrvsio
         then
     then
     dup $45 ( "E" )   = if newname clearfcount printdestination then
     dup $53 ( "S" )   = if sidetgl_switch printautoinc printdestination then
     dup $43 ( "C" )   = if sidetgl c@ if nextside  printdestination then then
     dup $41 ( "A" )   = if toggleautoinc printautoinc printdestination then
     dup $2B ( "+" )   = if nextfile printdestination then
     dup $2D ( "-" )   = if prevfile printdestination then
     dup $54 ( "T" )   = if txtmodesel printtxtmode then
     dup $46 ( "F" )   = if status then
     dup $44 ( "D" )   = if showdirectory menu then
     dup $51 ( "Q" )   = if exit then
     dup $47 ( "G" )   = if cdaccdir 0 = if txtgen c@ if wrtxt then then copy then
     dup $1B ( "ESC" ) = if menu then
     dup $4D ( "M" )   = if menu then
     dup $3F ( "?"   ) = if $ff debugflg ctoggle 1 38 at debugflg @ if $44 else $20 then  emit then  ( CS )
     drop
   }
   cls crson
;

: main
  init
  acc
  bye
;
